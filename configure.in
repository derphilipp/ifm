dnl Process this file with autoconf to produce a configure script.

dnl Initialize autoconf.
AC_INIT(ifm, 5.2, zondo42@googlemail.com)
AC_CONFIG_SRCDIR(src/ifm-main.c)
AC_CONFIG_AUX_DIR(etc)

dnl Initialize automake.
AM_INIT_AUTOMAKE(foreign)

dnl Check for Vars library.
if test -d $srcdir/vars; then
    AC_CONFIG_SUBDIRS(vars)
    AC_SUBST(VARS_CFLAGS)
    AC_SUBST(VARS_LIBS)
    VARS_CFLAGS='-I$(top_srcdir)/vars/src'
    VARS_LIBS='-L$(top_builddir)/vars/src/.libs -lvars -lm'
else
    AM_CONFIG_LIB(Vars, VARS, vars-config, 3.1, the Vars library is required -- get it from www.freewebtown.com/zondo/programs)
fi

dnl Checks for programs.
AC_PROG_CC
AC_PROG_YACC
AM_PROG_LEX
AC_PROG_INSTALL
AC_PROG_MAKE_SET
AC_PROG_RANLIB

dnl Checks for header files.
AC_HEADER_STDC

dnl Check for cygwin.
AC_CANONICAL_HOST

case $host_os in
     *cygwin*) CYGWIN=yes;;
     *)        CYGWIN=no;;
esac 

if test "$IFM_CYGWIN" != ""; then
   AC_MSG_NOTICE(overriding Cygwin detection)
   CYGWIN=yes
fi

if test $CYGWIN = yes; then
   AC_DEFINE(CYGWIN, 1, Whether building using Cygwin on Windoze)
fi

AM_CONDITIONAL(CYGWIN, test $CYGWIN = yes)

dnl Check for wish.
AC_PATH_PROGS(WISH, wish8.0 wish80 wish, no)

tkifm=tkifm
AC_SUBST(tkifm)

if test $WISH = no; then
   AC_MSG_WARN(wish not found -- not building wish scripts)
   tkifm=
fi

dnl Check for perl.
AC_PATH_PROGS(PERL, perl5.002 perl5 perl, no)

ifm2dev=ifm2dev
AC_SUBST(ifm2dev)

ifm2tex=ifm2tex
AC_SUBST(ifm2tex)

scr2ifm=scr2ifm
AC_SUBST(scr2ifm)

if test $PERL = no; then
   AC_MSG_WARN(perl not found -- not building perl scripts)
   ifm2dev=
   ifm2tex=
   scr2ifm=
fi

dnl Write Makefiles.
AC_OUTPUT(
Makefile
demos/Makefile
doc/Makefile
doc/cover.ifm
fig/Makefile
lib/Makefile
progs/Makefile
src/Makefile
tests/Makefile
)
