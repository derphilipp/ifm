dnl Configuration template for IFM.

dnl Process this file with autoconf to produce a configure script.
AC_INIT(ifm, 5.2, zondo@pillock.freeserve.co.uk)
AC_CONFIG_SRCDIR(src/ifm-main.c)
AC_CONFIG_AUX_DIR(etc)

AM_INIT_AUTOMAKE(ifm, 5.2)
AM_CONFIG_HEADER(etc/config.h)
AC_CONFIG_SUBDIRS(vars)

dnl Check host system.
AC_CANONICAL_HOST

case $host_os in
     *cygwin*) CYGWIN=yes;;
     *)        CYGWIN=no;;
esac 

if test "$IFM_CYGWIN" != ""; then
   AC_MSG_NOTICE(overriding Cygwin detection)
   CYGWIN=yes
fi

if test $CYGWIN = yes; then
   AC_DEFINE(CYGWIN, 1, Whether building using Cygwin on Windoze)
fi

AM_CONDITIONAL(CYGWIN, test $CYGWIN = yes)

dnl Checks for programs.
AC_PROG_CC
AC_PROG_YACC
AM_PROG_LEX
AC_PROG_INSTALL
AC_PROG_RANLIB
AC_PROG_MAKE_SET
AM_PROG_LIBTOOL

dnl Checks for header files.
AC_HEADER_STDC

dnl Check for Vars library.
AC_PATH_PROG(VARS_CONFIG, vars-config, no)

if test $VARS_CONFIG != no; then
   VARS_CFLAGS="`$VARS_CONFIG --cflags`"
   AC_SUBST(VARS_CFLAGS)
   VARS_LIBS="`$VARS_CONFIG --libs`"
   AC_SUBST(VARS_LIBS)
else
   AC_MSG_ERROR(can't find Vars library)
fi

dnl Checks for other libraries.
AC_CHECK_LIB(m, sin)

dnl Check for wish.
AC_PATH_PROGS(WISH, wish8.0 wish80 wish, no)

tkifm=tkifm
AC_SUBST(tkifm)

if test $WISH = no; then
   AC_MSG_WARN(wish not found -- not building wish scripts)
   tkifm=
fi

dnl Check for perl.
AC_PATH_PROGS(PERL, perl5.002 perl5 perl, no)

ifm2dev=ifm2dev
AC_SUBST(ifm2dev)

ifm2tex=ifm2tex
AC_SUBST(ifm2tex)

scr2ifm=scr2ifm
AC_SUBST(scr2ifm)

if test $PERL = no; then
   AC_MSG_WARN(perl not found -- not building perl scripts)
   ifm2dev=
   ifm2tex=
   scr2ifm=
fi

dnl Write Makefiles.
AC_OUTPUT(
Makefile
demos/Makefile
doc/Makefile
fig/Makefile
gpp/Makefile
lib/Makefile
progs/Makefile
src/Makefile
tests/Makefile
)
