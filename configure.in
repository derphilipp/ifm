dnl Process this file with autoconf to produce a configure script.
AC_INIT(src/ifm-main.c)
AC_CONFIG_AUX_DIR(config)

AM_INIT_AUTOMAKE(ifm, 3.1)
AM_CONFIG_HEADER(config.h)
AC_CONFIG_SUBDIRS(vars)

dnl Checks for programs.
AC_PROG_CC
AC_PROG_YACC
AM_PROG_LEX
AC_PROG_INSTALL
AC_PROG_RANLIB
AC_PROG_MAKE_SET

dnl Checks for header files.
AC_HEADER_STDC

dnl Checks for libraries.
AC_CHECK_LIB(m, sin)

dnl Check for wish.
AC_PATH_PROGS(WISH, wish wish8.0 wish80, no)

tkifm=tkifm
AC_SUBST(tkifm)
if test $WISH = no; then
   AC_MSG_WARN(wish not found -- not building tkifm)
   tkifm=
fi

dnl Check for perl.
AC_PATH_PROG(PERL, perl, no)

scr2ifm=scr2ifm
AC_SUBST(scr2ifm)
if test $PERL = no; then
   AC_MSG_WARN(perl not found -- not building scr2ifm)
   scr2ifm=
fi

dnl Check for executable suffix.
AC_CYGWIN

dnl Check whether to install vars library.
AC_ARG_ENABLE(vars-install,
[  --enable-vars-install   Install vars support library],
[case "${enableval}" in
  yes) install_vars=yes ;;
  no)  install_vars=no  ;;
  *)   AC_MSG_ERROR(bad value ${enableval} for --enable-vars-install) ;;
esac],[install_vars=no])

AC_CACHE_CHECK(whether to install support library, ac_cv_install_vars,
[ac_cv_install_vars=$install_vars])

dnl Add common Makefile rules.
common_rules=$srcdir/config/Makefile.add
AC_SUBST_FILE(common_rules)

dnl Write Makefiles.
AC_OUTPUT(
Makefile
src/Makefile
lib/Makefile
doc/Makefile
scr2ifm/Makefile
tkifm/Makefile
tests/Makefile
)
