dnl Configuration template for IFM.

dnl Process this file with autoconf to produce a configure script.
AC_INIT(src/ifm-main.c)
AC_CONFIG_AUX_DIR(etc)

AM_INIT_AUTOMAKE(ifm, 5.0)
AM_CONFIG_HEADER(etc/config.h)
AC_CONFIG_SUBDIRS(vars)

AC_DEFINE(EMAIL, "zondo@pillock.freeserve.co.uk")

dnl Checks for programs.
AC_PROG_CC
AC_PROG_YACC
AM_PROG_LEX
AC_PROG_INSTALL
AC_PROG_RANLIB
AC_PROG_MAKE_SET

dnl Checks for header files.
AC_HEADER_STDC

dnl Checks for libraries.
AC_CHECK_LIB(m, sin)

dnl Check for wish.
AC_PATH_PROGS(WISH, wish8.0 wish80 wish, no)

tkifm=tkifm
AC_SUBST(tkifm)

if test $WISH = no; then
   AC_MSG_WARN(wish not found -- not building wish scripts)
   tkifm=
fi

dnl Check for perl.
AC_PATH_PROGS(PERL, perl5.002 perl5 perl, no)

ifm2dev=ifm2dev
AC_SUBST(ifm2dev)

ifm2tex=ifm2tex
AC_SUBST(ifm2tex)

scr2ifm=scr2ifm
AC_SUBST(scr2ifm)

if test $PERL = no; then
   AC_MSG_WARN(perl not found -- not building perl scripts)
   ifm2dev=
   ifm2tex=
   scr2ifm=
fi

dnl Check whether to install vars library.
AC_ARG_ENABLE(vars-install,
AC_HELP_STRING(--enable-vars-install, install vars support library),
VARS_INSTALL=$enableval, VARS_INSTALL=no)
export VARS_INSTALL

dnl Write Makefiles.
AC_OUTPUT(
Makefile
demos/Makefile
doc/Makefile
fig/Makefile
gpp/Makefile
lib/Makefile
progs/Makefile
src/Makefile
tests/Makefile
)
